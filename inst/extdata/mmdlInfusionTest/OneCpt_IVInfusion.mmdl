## Description: one-compartment model with IV infusion
##
##				The model is fitted using the model using the default values for the relevant NLME engine arguments.
##
## 				Note: 	the default values for the relevant NLME engine arguments are chosen based on the model, type ?engineParams for details.
##       				For example, for this example, FOCE-ELS is the default method for estimation, and Sandwich is the default method for
##       				standard error calculations.


## DATA OneCpt_IVInfusion.csv
## MAP id = Subject time = Time A1 = Dose A1_Rate = Rate CObs

## MODEL
test(){
	##=========================================
	## PK model
	##=========================================
	cfMicro(A1, Cl / V)
	dosepoint(A1)
	C = A1 / V

	##=========================================
	## Residual error model
	##=========================================
	error(CEps = 0.1)
	observe(CObs = C * (1 + CEps))

	##=========================================
	## Model parameters
	##=========================================
	stparm(V = tvV * exp(nV))
	stparm(Cl = tvCl * exp(nCl))

	# fixed effects
	fixef(tvV = c(, 5, ))
	fixef(tvCl = c(, 1, ))

	# random effects
	ranef(diag(nV, nCl) = c(1, 1))
}

## ESTARGS
numIterations = 1
stdErr = "None"
